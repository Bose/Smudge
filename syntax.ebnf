state-machine = state-machine-name empty "{" empty transition-specifier empty "}" empty
sep = "-" / "_"
symbol = "!" / "#" / "$" / "%" / "&" / "'" / "(" / ")" / "*" / "+" / "," / "." / "/" / "{" / "|" / "}" / "~" / "[" / "\" / "]" / "^" / "`"
eol = LF
lwsp = *(WSP / eol)
empty = comment / lwsp
space = (WSP / eol / comment) empty
non-newline = ALPHA / WSP / DIGIT / sep / symbol
comment = "/" "/" *non-newline eol
quoted = DQUOTE 1*(ALPHA / WSP / DIGIT / sep / symbol) DQUOTE
identifier = 1*(ALPHA / DIGIT / "-") / 2*(ALPHA / DIGIT / sep) / quoted
any = "_"
state-machine-name = identifier
transition-specifier = to-state / state-list
state = state-title empty enter-function empty event-handler-list empty exit-function
event-handler-list = "[" empty event-handler empty *("," empty event-handler empty) "]"
enter-function = [function-call space]
exit-function = [function-call]
state-list = state *(empty "," empty state)
function-call = "@" c-identifier
nondigit = ALPHA / "_"
c-identifier = nondigit *(nondigit / DIGIT)
event-handler = (event-name / any) empty (to-state / (dash empty))
to-state = arrow empty state-name
dash = "-" [side-effect-container] "-"
state-name = identifier
state-title = state-name / any
arrow = "-" [side-effect-container] "-" ">"
side-effect-container = "(" empty [side-effect-list] empty ")"
side-effect-list = side-effect [empty "," empty side-effect]
side-effect = function-call / typed-function-call / qualified-event
typed-function-call = function-call empty ":" empty qualified-event
qualified-event = [state-machine-name "."] event-name
event-name = identifier

;ALPHA = %x41-5A / %x61-7A
;DIGIT = %x30-39
;DQUOTE = %x22
;SP = %x20
;HTAB = %x09
;WSP = SP / HTAB
;CR = %x0D
;LF = %x0A
